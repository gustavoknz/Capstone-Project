apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            Properties keyProps = new Properties()
            assert localProps['keystore.props.file']

            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

            storeFile file(keyProps["RELEASE_STORE_FILE"])
            keyAlias keyProps["RELEASE_KEY_ALIAS"]
            storePassword keyProps["RELEASE_STORE_PASSWORD"]
            keyPassword keyProps["RELEASE_KEY_PASSWORD"]
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.bora.gustavo"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'GOOGLE_SEC_API_KEY', GoolgeAPIKey
        it.buildConfigField 'String', 'FIREBASE_URL', FirebaseUrl
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'android.arch.core:runtime:1.1.1'
    implementation 'com.android.support:animated-vector-drawable:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-media-compat:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    // Firebase Realtime Database
    implementation 'com.google.firebase:firebase-auth:16.1.0'
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-database:16.0.5'
    implementation 'com.google.firebase:firebase-storage:16.0.5'
    // Butter knife
    implementation 'com.jakewharton:butterknife:9.0.0-rc3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc3'
    // Dexter runtime permissions
    implementation 'com.karumi:dexter:4.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test:rules:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation('org.mockito:mockito-android:2.23.0') {
        exclude group: 'net.bytebuddy', module: 'byte-buddy'
        exclude group: 'net.bytebuddy', module: 'byte-buddy-android'
        exclude group: 'net.bytebuddy', module: 'byte-buddy-agent'
    }
    // Explicitly include byte-buddy 1.8.22 to fix a build error with Mockito 2.23
    androidTestImplementation 'net.bytebuddy:byte-buddy:1.8.22'
    androidTestImplementation 'net.bytebuddy:byte-buddy-android:1.8.22'
    androidTestImplementation 'net.bytebuddy:byte-buddy-agent:1.8.22'
}

apply plugin: 'com.google.gms.google-services'
